module test_FireWeather
  !
  ! DESCRIPTION:
  !		Test the FATES fire weather portion of the SPITFIRE model
  !
  use FatesConstantsMod, only : r8 => fates_r8
  use SFFireWeather,     only : fire_weather
  use SFParamsMod,       only : SF_val_fdi_a, SF_val_fdi_b
  use funit

  implicit none

  @TestCase
  type, extends(TestCase) :: TestFireWeather
    
    type(fire_weather) :: fireWeather
    
    contains
      procedure :: setUp
      procedure :: tearDown 
  end type TestFireWeather

  real(r8), parameter :: tol = 1.e-13_r8

  contains 

    subroutine setUp(this)
      class(TestFireWeather), intent(inout) :: this
      call this%fireWeather%Init(1)
      SF_val_fdi_a = 17.62_r8
      SF_val_fdi_b = 243.12_r8
    end subroutine setUp

    subroutine tearDown(this)
      class(TestFireWeather), intent(inout) :: this 
    end subroutine tearDown

    @Test 
    subroutine zero_NI_rain(this)
      ! test that over 3 mm of rain is 0.0
      class(TestFireWeather), intent(inout) :: this ! fire weather object
      real(r8)                              :: NI   ! Nesterov Index 

      NI = this%fireWeather%NesterovIndex(25.0_r8, 3.1_r8, 10.0_r8)
      @assertEqual(NI, 0.0_r8, tolerance=tol)
    end subroutine zero_NI_rain

    @Test
    subroutine NI_rain_min(this)
      ! test that at 3 mm is over zero
      class(TestFireWeather), intent(inout) :: this ! fire weather object
      real(r8)                              :: NI   ! Nesterov Index 

      NI = this%fireWeather%NesterovIndex(25.0_r8, 3.0_r8, 10.0_r8)
      @assertGreaterThan(NI, 0.0_r8, tolerance=tol)
    end subroutine NI_rain_min

    @Test
    subroutine NI_not_negative(this)
      ! test that NI is not negative
      class(TestFireWeather), intent(inout) :: this ! fire weather object
      real(r8)                              :: NI   ! Nesterov Index 

      NI = this%fireWeather%NesterovIndex(-30.0_r8, 0.0_r8, 99.0_r8)
      @assertEqual(NI, 0.0_r8, tolerance=tol)
    end subroutine NI_not_negative

  end module test_FireWeather